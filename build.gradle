plugins {
	id "fabric-loom" version "1.4-SNAPSHOT"
	id "com.github.johnrengelman.shadow" version "7.0.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

configurations {
	shadow
	modImplementation.extendsFrom shadow
}

repositories {
	gradlePluginPortal()

	maven {
		name = "lukflug"
		url = "https://lukflug.github.io/maven/"
	}

	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases' // ladysnake.jfrog.io/artifactory/mods
		content {
			includeGroup 'io.github.ladysnake' // 1.20.1 is org.ladysnake ( same for dependencies below)
			includeGroupByRegex 'io\\.github\\.onyxstudios.*'
		}
	}

	flatDir {
		dirs 'libs'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

	modImplementation "io.github.ladysnake:satin:${satin_version}"
	include "io.github.ladysnake:satin:${satin_version}"

	shadow "com.lukflug:panelstudio:${panelstudio_version}"
	shadow "com.lukflug:panelstudio-mc20:${panelstudio_versionmc}"
}

loom {
	accessWidenerPath = file("src/main/resources/postman.aw")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release.set(17)
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

shadowJar {
	// classifier = "dev"
	configurations = [project.configurations.shadow]
	relocate "com.lukflug.panelstudio","com.lukflug.panelstudio_0_2_4"
}

remapJar {
	dependsOn shadowJar
	inputFile = shadowJar.archiveFile
}
